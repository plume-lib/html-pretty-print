plugins {
  id 'java'
  id 'application'

  // To create a fat jar build/libs/...-all.jar, run: ./gradlew shadowJar
  id 'com.github.johnrengelman.shadow' version '7.1.2'

  // Code formatting; defines targets "spotlessApply" and "spotlessCheck"
  id "com.diffplug.spotless" version "6.12.0"

  // Error Prone linter
  id("net.ltgt.errorprone") version "3.0.1"

  // Checker Framework pluggable type-checking
  id 'org.checkerframework' version '0.6.20'
}

repositories {
  mavenCentral()
}

dependencies {
  implementation 'com.google.guava:guava:31.1-jre'

  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.1'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'

  // https://mvnrepository.com/artifact/org.ccil.cowan.tagsoup/tagsoup
  implementation group: 'org.ccil.cowan.tagsoup', name: 'tagsoup', version: '1.2.1'

  // https://mvnrepository.com/artifact/xom/xom
  implementation group: 'xom', name: 'xom', version: '1.3.8'
}

application {
  mainClass = 'org.plumelib.htmlprettyprint.HtmlPrettyPrint'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

test {
  useJUnitPlatform {
    includeEngines 'junit-jupiter'
    excludeEngines 'junit-vintage'
  }
}

spotless {
  format 'misc', {
    // define the files to apply `misc` to
    target '*.gradle', '*.md', '.gitignore'

    // define the steps to apply to those files
    trimTrailingWhitespace()
    indentWithSpaces(2)
    endWithNewline()
  }
  java {
    targetExclude("**/WeakIdentityHashMap.java")
    googleJavaFormat()
    formatAnnotations()
  }
}

/// Error Prone linter

dependencies {
  errorprone("com.google.errorprone:error_prone_core:2.16")
}
tasks.withType(JavaCompile).configureEach {
  // "-processing" avoids javac warning "No processor claimed any of these annotations".
  options.compilerArgs << "-Xlint:all,-processing" << "-Werror"
  options.errorprone {
    enabled = JavaVersion.current() != JavaVersion.VERSION_1_8
    disable("ReferenceEquality") // Use Interning Checker instead.
  }
}

/// Checkstyle linter

// Run by `gradle check`, which is run by `gradle build`

apply plugin: 'checkstyle'

ext.checkstyleVersion = '10.5.0'
configurations {
  checkstyleConfig
}
dependencies {
  checkstyleConfig("com.puppycrawl.tools:checkstyle:${checkstyleVersion}") { transitive = false }
}
checkstyle {
  toolVersion "${checkstyleVersion}"
  config = resources.text.fromArchiveEntry(configurations.checkstyleConfig, 'google_checks.xml')
  ignoreFailures = false
}

/// Checker Framework pluggable type-checking

apply plugin: 'org.checkerframework'

checkerFramework {
  checkers = [
    // No need to run CalledMethodsChecker, because ResourceLeakChecker does so.
    // 'org.checkerframework.checker.calledmethods.CalledMethodsChecker',
    'org.checkerframework.checker.formatter.FormatterChecker',
    'org.checkerframework.checker.index.IndexChecker',
    'org.checkerframework.checker.interning.InterningChecker',
    'org.checkerframework.checker.lock.LockChecker',
    'org.checkerframework.checker.nullness.NullnessChecker',
    'org.checkerframework.checker.regex.RegexChecker',
    'org.checkerframework.checker.resourceleak.ResourceLeakChecker',
    'org.checkerframework.checker.signature.SignatureChecker',
    'org.checkerframework.checker.signedness.SignednessChecker',
    'org.checkerframework.common.initializedfields.InitializedFieldsChecker',
  ]
  extraJavacArgs = [
    '-Werror',
    '-AcheckPurityAnnotations',
    '-ArequirePrefixInWarningSuppressions',
    '-AwarnUnneededSuppressions',
  ]
}

// To use a locally-built Checker Framework, run gradle with "-PcfLocal".
if (project.hasProperty("cfLocal")) {
  def cfHome = String.valueOf(System.getenv("CHECKERFRAMEWORK"))
  dependencies {
    compileOnly files(cfHome + "/checker/dist/checker-qual.jar")
    testCompileOnly files(cfHome + "/checker/dist/checker-qual.jar")
    checkerFramework files(cfHome + "/checker/dist/checker.jar")
  }
}

/// Javadoc

// Turn Javadoc warnings into errors.
javadoc {
  options.addStringOption('Xwerror', '-Xdoclint:all')
  options.addStringOption('private', '-quiet')
  if (JavaVersion.current().compareTo(org.gradle.api.JavaVersion.VERSION_1_9) >= 0) {
    options.addStringOption('source', '8')
  }
  doLast {
    if (JavaVersion.current() == JavaVersion.VERSION_1_8) {
      ant.replaceregexp(match:"@import url\\('resources/fonts/dejavu.css'\\);\\s*", replace:'',
        flags:'g', byline:true) {
        fileset(dir: destinationDir)
      }
    }
  }
}
check.dependsOn javadoc

configurations {
  requireJavadoc
}
dependencies {
  requireJavadoc "org.plumelib:require-javadoc:1.0.6"
}
task requireJavadoc(type: JavaExec) {
  description = 'Ensures that Javadoc documentation exists.'
  mainClass = "org.plumelib.javadoc.RequireJavadoc"
  classpath = configurations.requireJavadoc
  args "src/main/java"
}
check.dependsOn requireJavadoc

// Run by `gradle check`, which is run by `gradle build`
apply plugin: 'checkstyle'
checkstyle {
  // toolVersion = "10.5.0"
  ignoreFailures = false
  // configFile = file("${project.rootDir}/checkstyle.xml")
}

/// Emacs support

/* Make Emacs TAGS table */
task tags(type: Exec) {
  description "Run etags to create an Emacs TAGS table"
  commandLine "bash", "-c", "find src/ -name '*.java' | sort | xargs etags"
}
